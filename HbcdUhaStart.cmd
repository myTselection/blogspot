@SET DEBUG=0
@echo on
@IF %DEBUG%==0 echo off

:PARAMETERS
	SET UHA_FILE=%1
	SET UHA_FOLDER=%UHA_FILE%
	SET UHA_FOLDER=%UHA_FOLDER:.uha=%
	SET EXECUTE_FILE=%2
	SET EXECUTE_FILE=%EXECUTE_FILE:"=%
::REM "
	
	SET UHARC_EXE=..\uharc.exe
	SET UHARC_COMMANDs=x -y+ -d0
	SET UHARC_EXTRACT_FOLDER=%TEMP%
	SET UHA_FILE_SOURCE_FOLDER=files\
	SET HBCD_FORCE_EXTRACT_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_FORCE_EXTRACT.TXT
	SET FORCE_EXTRACT=0
	
	SET HBCD_EXPLORE_ONLY_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_EXPLORE_ONLY.TXT
	SET HBCD_CMD_ONLY_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_CMD_ONLY.TXT
	SET HBCD_SANDBOX_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_SANDBOX.TXT
	SET HBCD_SHOW_ONLINE_INFO_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_SHOW_ONLINE_INFO.TXT
	SET HBCD_ZIP_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_ZIP.TXT
	SET HBCD_UHARC_CHECK_FILE=%UHARC_EXTRACT_FOLDER%\HBCD_UHARC.TXT
	
	SET TITLE=%UHA_FOLDER%
	SET FOLDER=%UHARC_EXTRACT_FOLDER%\%UHA_FOLDER%
	SET PROGRAM=%EXECUTE_FILE%
	SET PROGRAM_NAME=%PROGRAM%
	SET PROGRAM_NAME=%PROGRAM_NAME:.exe=%
	SET PROGRAM_NAME=%PROGRAM_NAME:.bat=%
	SET PROGRAM_NAME=%PROGRAM_NAME:.cmd=%
	TITLE %PROGRAM_NAME%
:END_PARAMETERS

:FORCE_EXTRACT_CHECK
	IF EXIST "%HBCD_FORCE_EXTRACT_CHECK_FILE%" SET FORCE_EXTRACT=1
:END_FORCE_EXTRACT_CHECK

:EXTRACT
	echo Extracting ...
	IF %FORCE_EXTRACT%==1 %UHARC_EXE% %UHARC_COMMANDS% -o+ -t"%FOLDER%" %UHA_FILE_SOURCE_FOLDER%%UHA_FILE%
	IF NOT EXIST "%FOLDER%" %UHARC_EXE% %UHARC_COMMANDS% -t"%FOLDER%" %UHA_FILE_SOURCE_FOLDER%%UHA_FILE%
	@IF %DEBUG% EQU 1 pause
:END_EXTRACT

:USAGE_CHECK_EXPLORE_ONLY
	IF EXIST "%HBCD_EXPLORE_ONLY_CHECK_FILE%" GOTO EXPLORE 
:END_USAGE_CHECK_EXPLORE_ONLY
	
:USAGE_CHECK_CMD_ONLY
	IF EXIST "%HBCD_CMD_ONLY_CHECK_FILE%" GOTO CMD 
:END_USAGE_CHECK_CMD_ONLY
	
:USAGE_CHECK_SANDBOX
	IF EXIST "%HBCD_SANDBOX_CHECK_FILE%" GOTO SANDBOX 
:END_USAGE_CHECK_SANDBOX
	
:USAGE_CHECK_ONLINE_INFO
	IF EXIST "%HBCD_SHOW_ONLINE_INFO_CHECK_FILE%" GOTO SHOW_ONLINE_INFO 
:END_USAGE_CHECK_ONLINE_INFO
	
:USAGE_CHECK_ZIP
	IF EXIST "%HBCD_ZIP_CHECK_FILE%" GOTO ZIP 
:END_USAGE_CHECK_ZIP
	
:USAGE_CHECK_UHARC
	IF EXIST "%HBCD_UHARC_CHECK_FILE%" GOTO UHARC 
:END_USAGE_CHECK_UHARC

:USAGE_DEFAULT
	GOTO DEFAULT
:END_USAGE_DEFAULT

		
:EXPLORE
	if exist "%FOLDER%\%UHA_FOLDER%\%PROGRAM%" %SystemRoot%\explorer.exe /e, "%FOLDER%\%UHA_FOLDER%"
	if exist "%FOLDER%\%PROGRAM%" %SystemRoot%\explorer.exe /e, "%FOLDER%"
:END_EXPLORE
	GOTO END
	
:CMD
	if exist "%FOLDER%\%UHA_FOLDER%\%PROGRAM%" start "%PROGRAM%" cmd.exe /K PUSHD "%FOLDER%\%UHA_FOLDER%"
	if exist "%FOLDER%\%PROGRAM%" start "%PROGRAM%" cmd.exe /K PUSHD "%FOLDER%"
:END_CMD
	GOTO END
	
:SANDBOX
	start "sandboxie" /WAIT Sandboxie "%FOLDER%\%PROGRAM%"
:END_SANDBOX
	GOTO END
	
:SHOW_ONLINE_INFO
	start /B "Show online Info" "http://www.google.be/search?hl=nl&q=%PROGRAM_NAME%+PORTABLE&btnI=Ik+doe+een+gok&meta=&aq=f&oq=
:END_SHOW_ONLINE_INFO
	GOTO END
		
:ZIP
	echo Creating ZIP file for %PROGRAM_NAME%
	if not exist "%UHARC_EXTRACT_FOLDER%\7-zip\7-zip" %UHARC_EXE% %UHARC_COMMANDS% -t"%UHARC_EXTRACT_FOLDER%\7-zip" %UHA_FILE_SOURCE_FOLDER%\7-zip.uha
	IF %DEBUG%==1 echo "%UHARC_EXTRACT_FOLDER%\7-zip\7-zip" 7zip a -r -bd "%CD%\..\..\%PROGRAM_NAME%.zip" "%FOLDER%"
	@IF %DEBUG% EQU 1 pause
	start "" /WAIT /D"%UHARC_EXTRACT_FOLDER%\7-zip\7-zip" 7zip a -r -bd "%CD%\..\..\%PROGRAM_NAME%.zip" "%FOLDER%"
:END_ZIP
	GOTO END
		
:UHARC
	echo Creating UHA file for %PROGRAM_NAME%
	SET UHARC_COMMAND=a
		rem command: a= create archive and Add files
		rem          m= create archive and Move files	
	SET UHARC_SUBDIRS=-r+
		rem recuse: -r- no, -r+ yes	
	SET UHARC_OVERWRITE=-o+
		rem overwrite: -o- no, -o+ yes	
	SET UHARC_DISPLAY=-d2
		rem display: -d0 quiet, -d1 default, -d2 verbose
	SET CURR_PATH=%CD%
	SET UHARC_EXE_FULLPATH="%CURR_PATH%\%UHARC_EXE%"
	SET UHA_FILE_TO_CREATE="%CURR_PATH%\%UHA_FILE_SOURCE_FOLDER%%UHA_FILE%"
	@IF %DEBUG% EQU 1 pause
		
	if exist "%FOLDER%\%UHA_FOLDER%" pushd "%FOLDER%\" & %UHARC_EXE_FULLPATH% %UHARC_COMMAND% %UHARC_SUBDIRS% %UHARC_OVERWRITE% %UHARC_DISPLAY% %UHA_FILE_TO_CREATE% "%UHA_FOLDER%\*"
	if exist "%FOLDER%" pushd "%FOLDER%\" & %UHARC_EXE_FULLPATH% %UHARC_COMMAND% %UHARC_SUBDIRS% %UHARC_OVERWRITE% %UHARC_DISPLAY% %UHA_FILE_TO_CREATE% *
	@IF %DEBUG% EQU 1 pause
	"%CURR_PATH%\SpecialRunMode" DEFAULT
:END_UHARC
	GOTO END
	
:DEFAULT
	@IF %DEBUG% EQU 1 pause
	if exist "%FOLDER%\%PROGRAM%" goto DEFAULT_SUBFOLDER
	if exist "%FOLDER%\%UHA_FOLDER%\%PROGRAM%" goto DEFAULT_DOUBLE_SUBFOLDER
	echo Program not found
	start "%PROGRAM%" cmd.exe /K PUSHD "%UHARC_EXTRACT_FOLDER%"
:END_DEFAULT
	GOTO END

:DEFAULT_DOUBLE_SUBFOLDER
	@IF %DEBUG% EQU 1 pause
	start "%TITLE%" /D"%FOLDER%\%UHA_FOLDER%" "%PROGRAM%" %3 %4 %5 %6 %7 %8 %9
:END_DEFAULT_DOUBLE_SUBFOLDER
	GOTO END

:DEFAULT_SUBFOLDER
	@IF %DEBUG% EQU 1 pause
	start "%TITLE%" /D"%FOLDER%" "%PROGRAM%" %3 %4 %5 %6 %7 %8 %9
:END_DEFAULT_SUBFOLDER
	GOTO END

:END
	@IF %DEBUG% EQU 1 pause
	exit